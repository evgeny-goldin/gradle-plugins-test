
apply from: 'https://raw.github.com/evgeny-goldin/scripts/master/src/main/gradle/wrapper.gradle'

buildscript {

    final version = project.hasProperty( 'pluginsVersion' ) ? project.pluginsVersion : '0.2.1-SNAPSHOT'
    println "> Running Node.js plugin version [$version]"

    repositories { mavenLocal(); maven { url 'http://evgenyg.artifactoryonline.com/evgenyg/repo/' }}
    dependencies { classpath "com.github.goldin.plugins.gradle:node:$version" }
}

defaultTasks 'clean', 'cleanModules', 'start', 'restartall', 'stop', 'stopall'


int portNumberForProject( Project p ){ 3000 + ( Math.abs( p.name.hashCode()) % 100 ) }
void assertEqualFiles( Project p, String fileName1, String fileName2 )
{
    final f1 = p.file( fileName1 )
    final f2 = p.file( fileName2 )
    assert f1.text == f2.text, "[$f1.canonicalPath] != [$f2.canonicalPath]"
    println "[$f1.canonicalPath] == [$f2.canonicalPath]"
}


subprojects {
    Project p ->

    apply plugin: 'node'

    clean.doFirst {
        if ( System.getProperty( 'cleanNode' ) != null )
        {
            final deleteFiles = [ new File( System.getProperty( 'user.home' ), '.npm' ),
                                  new File( System.getProperty( 'user.home' ), '.nvm' ) ]

            println "Deleting $deleteFiles"
            project.delete( deleteFiles )
        }
    }

    node {
        nodeVersion            = 'latest'
        cleanWorkspace         = true
        cleanWorkspaceCommands = [ 'git checkout -f config' ]
        checkContent           = p.name + (( p.name == 'hello-js'                     ) ? '|yhn|890|' :
                                           ( p.name == 'hello-js-port-number'         ) ? '|undefined|undefined|' :
                                           ( p.name == 'hello-coffee-port-number'     ) ? '|zaq|123|' :
                                           ( p.name == 'hello-js-configs-new-keys'    ) ? '|OLM|457|false|false' :
                                           ( p.name == 'hello-js-configs-update-keys' ) ? '|IMN|784|true|true' :
                                                                                          '' )
        pidOnlyToStop   = false
        portNumber      = portNumberForProject( p )
        scriptArguments = "--port $portNumber"
        configs         = [[ 'config/development.json' : file( "${ project.name }.json" ) ],
                           [ 'config/development.json' : [ port : portNumber ]]]
    }
}


project ( ':hello-js'                 ) { node { configsNewKeys = 'fail'   }}
project ( ':hello-js-port-number'     ) { node { configsNewKeys = 'ignore' }}
project ( ':hello-coffee-port-number' ) { node { configsNewKeys = 'create' }}


project ( ':hello-js-configs-new-keys'    ) {
    Project p ->

    node {
        configsNewKeys = 'create'
        configs        = [[ 'config/development.json'   : file( "${ project.name }.json" ) ],
                          [ 'config/development.json'   : [ port : portNumber ]],
                          [ 'config/development.json'   : file( "${ project.name }-2.json" ) ],
                          [ 'config/development-2.json' : file( "${ project.name }.properties" ) ]]
    }

    setup << {
        assertEqualFiles( p, 'config/development.json',   'config/development-result.json' )
        assertEqualFiles( p, 'config/development-2.json', 'config/development-2-result.json' )
    }
}

project ( ':hello-js-configs-update-keys' ) {
    Project p ->

    node {
        configsNewKeys = 'fail'
        configs        = [[ 'config/development.json'   : file( "${ project.name }.json" ) ],
                          [ 'config/development.json'   : [ port : portNumber ]],
                          [ 'config/development-2.json' : file( "${ project.name }.properties" ) ]]
    }

    setup << {
        assertEqualFiles( p, 'config/development.json',   'config/development-result.json' )
        assertEqualFiles( p, 'config/development-2.json', 'config/development-2-result.json' )
    }
}

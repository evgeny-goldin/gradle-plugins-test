
apply plugin: 'idea'
apply plugin: 'crawler'


ext.codenarcSources      =  'codenarc duplicates'.tokenize().collect { "$projectDir/$it/src/main/groovy" }
ext.codenarcRuleSetFiles = [ "$projectDir/codenarc.groovy" ]
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/codenarc/CodeNarc.gradle'


defaultTasks 'clean', 'codenarc', 'duplicates', 'build', 'about'


group = 'com.github.goldin.plugins.gradle'


buildscript {

    final pluginsVersion = '0.2-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
        mavenRepo url: 'https://oss.sonatype.org/content/repositories/orggcontracts-025/'
    }

    dependencies {
        classpath "com.github.goldin.plugins.gradle:about:$pluginsVersion",
                  "com.github.goldin.plugins.gradle:crawler:$pluginsVersion",
                  "com.github.goldin.plugins.gradle:duplicates:$pluginsVersion",
                  "com.github.goldin.plugins.gradle:kotlin:$pluginsVersion",
                  "com.github.goldin.plugins.gradle:teamcity:$pluginsVersion"
    }
}


idea.project {
    jdkName = '1.7'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


idea.module {

    downloadSources = true
    downloadJavadoc = false

    iml.withXml {
        provider ->
        provider.node.component.content.first().with {
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/gradle' ])
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/out'    ])
        }
        provider.node.component.first().appendNode( 'orderEntry', [ type : 'library', name : 'Groovy', level: 'application' ])
    }
}


crawler {
    baseUrl            = 'evgeny-goldin.com'
    verbose            = true
    failOnBrokenLinks  = true
    checkExternalLinks = true
    ignoredStatusCodes = [ 500 ]
    linksMapFile       = file( 'links.txt' )
    newLinksMapFile    = file( 'newLinks.txt' )
    minimumBytes       = 2 * 1024 * 1024
    minimumLinks       = 2700
    ignoredEndsWith    = 'redirect=no /feed/ /blog/wp-comments-post.php'.tokenize()
    ignoredContains    = [ 'truezip.dev.java.net',
                           'delicious.com/evgenyg',
                           'repo.gradle.org',
                           'repo.grails.org',
                           'repo.jenkins-ci.org',
                           'repo.springsource.org',
                           'repo.typesafe.com',
                           'repository.jetbrains.com' ] +
                         '... Talk: Special: printable= oldid= action= /blog/tag/ wp-app.php xmlrpc.php replytocom='.tokenize()
}


subprojects {

    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'about'
    apply plugin: 'duplicates'
    apply plugin: 'kotlin'


    clean { delete( "$rootDir/out", "$rootDir/build" ) }


    repositories {
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }

    dependencies {
        groovy      'org.codehaus.groovy:groovy-all:2.0.5'
        compile     'com.github.goldin.rest:youtrack:0.1-SNAPSHOT'
        testCompile ( 'junit:junit:4.10' ){ exclude group: 'org.hamcrest' }
        testRuntime 'ch.qos.logback:logback-classic:1.0.7'
    }
}


project ( ':kotlin:java-depends-on-kotlin' ) {
    dependencies.compile files( compileKotlin.destinationDir )
    compileJava.dependsOn compileKotlin
}


project ( ':kotlin:kotlin-depends-on-java' ) {
    compileKotlin.  dependsOn compileJava
    compileTestJava.dependsOn compileKotlin
}


task wrapper( type: Wrapper ) { gradleVersion = '1.2' }

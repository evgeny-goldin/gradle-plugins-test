apply plugin: 'idea'


defaultTasks 'clean', 'build', 'about', 'duplicates'


group = 'com.github.goldin.plugins'


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


idea.module {

    downloadSources = true
    downloadJavadoc = false

    iml.withXml {
        provider ->
        provider.node.component.content.first().with {
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/gradle' ])
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/out'    ])
        }
    }
}


buildscript {

    final pluginsVersion = '0.1.4-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }

    dependencies {
        classpath "com.github.goldin.plugins:gradle:$pluginsVersion",
                  "com.github.goldin.plugins:kotlin:$pluginsVersion",
                  "com.github.goldin.plugins:teamcity:$pluginsVersion"
    }
}


subprojects {

    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'about'
    apply plugin: 'duplicates'


    clean { delete( "$rootDir/out", "$rootDir/build" ) }


    repositories {
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }

    dependencies {
        compile     'com.github.goldin.rest:youtrack:0.1-SNAPSHOT'
        testCompile ( 'junit:junit:4.10' ){ exclude group: 'org.hamcrest' }
        testRuntime 'ch.qos.logback:logback-classic:1.0.6'
    }
}


project ( ':java-depends-on-kotlin' ) {
    dependencies.compile files( compileKotlin.destinationDir )
    compileJava.dependsOn compileKotlin
}

project ( ':kotlin-depends-on-java' ) {
    compileKotlin.  dependsOn compileJava
    compileTestJava.dependsOn compileKotlin
}

task wrapper( type: Wrapper ) { gradleVersion = '1.0' }
